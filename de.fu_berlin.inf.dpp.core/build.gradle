eclipse.project {
  natures 'org.eclipse.pde.PluginNature'
}

dependencies {
  releaseDep 'commons-codec:commons-codec:1.3'
  releaseDep 'commons-io:commons-io:2.0.1'
  releaseDep 'org.apache.commons:commons-lang3:3.8.1'
  releaseDep 'javax.jmdns:jmdns:3.4.1'
  releaseDep 'org.picocontainer:picocontainer:2.11.2'
  releaseDep 'org.igniterealtime.smack:smack:3.4.1'
  releaseDep 'org.igniterealtime.smack:smackx:3.4.1'
  releaseDep 'xpp3:xpp3:1.1.4c'
  releaseDep 'com.thoughtworks.xstream:xstream:1.4.10'
  releaseDep 'org.gnu.inet:libidn:1.15'

  releaseDep (log4jVersion) {
      exclude group: 'com.sun.jmx', module: 'jmxri'
      exclude group: 'com.sun.jdmk', module: 'jmxtools'
      exclude group: 'javax.jms', module: 'jms'
    }
  // The used version is a SNAPSHOT version which is not available!
  // TODO: use real release
  releaseDep files('libs/weupnp.jar')

  compile configurations.releaseDep
  testCompile configurations.testConfig
}

sourceSets.main.java.srcDirs = ['src', 'patches']
sourceSets.test.java.srcDir 'test/junit'

jar {
  manifest {
      def manif = "${projectDir}/META-INF/MANIFEST.MF"
      if (new File(manif).exists()) {
          from (manif) 
      }
  }

  from sourceSets.main.output

  // copy release dependencies into jar
  into('lib') {
    from configurations.getByName('releaseDep')
  }
}


// Avoid issue GH issue #98
tasks.withType(Test) {
  enableAssertions = false
}

task testJar(type: Jar) {
  classifier = 'tests'
  from sourceSets.test.output
}

artifacts {
  testing testJar
}

sonarqube {
  properties {
    property 'sonar.projectName', 'Saros/Core'
    property 'sonar.projectDescription', 'Saros core business logic and utilities'
    property 'sonar.projectVersion', '0.1.0.DEVEL'
  }
}
