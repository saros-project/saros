buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "gradle.plugin.org.jetbrains.intellij.plugins:gradle-intellij-plugin:0.3.4"
  }
}

apply plugin: 'org.jetbrains.intellij'

dependencies {
  compile project(':de.fu_berlin.inf.dpp.core')
  compile project(':de.fu_berlin.inf.dpp.ui')
  compile project(path: ':de.fu_berlin.inf.dpp.ui', configuration: 'testing')

  compile 'org.easytesting:fest-assert:1.2'
  compile 'org.easytesting:fest-reflect:1.2'
  compile 'org.easytesting:fest-swing:1.2.1'
  compile 'org.easytesting:fest-util:1.1.3'

  testCompile project(path: ':de.fu_berlin.inf.dpp.core', configuration: 'testing')
  testCompile configurations.testConfig
}

sourceSets {
  main {
    java.srcDir 'src'
    resources.srcDir 'resources'

    resources {
      srcDir 'src'
      exclude '**/*.java'
    }
  }

  test.java.srcDir 'test/junit'
}

test {
  exclude '**/Abstract*'
}

jar {
  manifest {
    instruction 'Created-By', 'IntelliJ IDEA'
    instruction 'Manifest-Version', '1.0'
  }
  version = null
  into ('META-INF')
  {
    from 'META-INF/plugin.xml'
  }
  from 'src/saros.log4j.properties'
  from 'log4j2.properties'

  from sourceSets.main.output
}

intellij {
  // Download intellij only if the home property is not set
  if (project.hasProperty('intellijHome')) {
    localPath = intellijHome
  } else {
    version = 'IC-2018.2'
  }
  pluginName 'Saros'
  configureDefaultDependencies = false

  // determine new sandbox dir
  Integer sandboxCount = 1
  Closure getSandboxDirPath = {->"${project.buildDir}/idea-sandbox$sandboxCount"}
  String newSandboxDirPath = getSandboxDirPath()

  while (new File(newSandboxDirPath).exists()) {
    newSandboxDirPath = getSandboxDirPath() as File
    sandboxCount++
  }
  sandboxDirectory = newSandboxDirPath
}

afterEvaluate {
  dependencies {
    compile intellij {exclude 'picocontainer*.jar', 'junit*.jar'}
  }
}

artifacts {
  archives buildPlugin
}

sonarqube {
  properties {
    property 'sonar.projectName', 'Saros/Intellij'
    property 'sonar.projectDescription', 'Saros plugin for IntelliJ IDEA'
    property 'sonar.projectVersion', '0.1.0.DEVEL'
  }
}
