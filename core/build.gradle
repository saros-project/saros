configurations {
  plain
}

sarosEclipse {
  manifest = file('META-INF/MANIFEST.MF')
  createBundleJar = true
  addPdeNature = true
}

dependencies {
  releaseDep 'commons-codec:commons-codec:1.3'
  releaseDep 'commons-io:commons-io:2.0.1'
  releaseDep 'org.apache.commons:commons-lang3:3.8.1'

  releaseDep 'javax.jmdns:jmdns:3.4.1'
  releaseDep project(path: ':saros.picocontainer', configuration: 'shadow')
  releaseDep 'xpp3:xpp3:1.1.4c'
  releaseDep 'com.thoughtworks.xstream:xstream:1.4.10'
  releaseDep 'org.gnu.inet:libidn:1.15'

  addLog4JDependency(configurations.releaseDep)
  // The used version is a SNAPSHOT version which is not available!
  // TODO: use real release
  releaseDep files('libs/weupnp.jar')
  // Workaround until we updated to a newer smack version
  releaseDep files('libs/smack-3.4.1.jar')
  releaseDep files('libs/smackx-3.4.1.jar')

  compile configurations.releaseDep
  plain configurations.compile
  testCompile configurations.testConfig
}

sourceSets.main.java.srcDirs = ['src', 'patches']
sourceSets.test.java.srcDir 'test/junit'

// Jar tasks

task testJar(type: Jar) {
  classifier = 'tests'
  from sourceSets.test.output
}

// Jar containing only the core code (the default jar is an osgi bundle
// containing a lib dir with all dependency jars)
task plainJar(type: Jar) {
  manifest {
    from 'META-INF/MANIFEST.MF'
  }
  from sourceSets.main.output
  classifier = 'plain'
}

artifacts {
  testing testJar
  plain plainJar
}